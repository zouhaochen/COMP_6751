% start S
# ###################
# Grammar Productions
# ###################
#
# Common Attributes:
#   TENSE  -> tense = inf, pres, past, future, pastpart
#   NUM    -> number of subject = sg, pl
#   PERSON -> subject person = 1, 2, 3
#   SENTIMENT  -> sentiment = positive, negative, neutral
#
# Other Attributes:
# S:
#   +-INV -> inversion?
# V:
#   SUBCAT -> verb subcategory = trans, intrans, clause, dative
#   +-AUX -> auxiliary verb?
#   +-passive -> passive mode
# RB:
#   +-negation -> negation?
# CC:
#   +-and -> and relation?
#   +-but -> but relation?
#   +-or  -> or relation?
# WP:
#   +-wh -> heading by 'wh'
# IN:
#   +-of -> of


# S expansion productions
S[-INV, SENTIMENT=?s] -> NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] VP[TENSE=?t, NUM=?n, PERSON=?p, SENTIMENT=?s]

## e.g. a perfect example
S[-INV, SENTIMENT=?s] -> NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s]

## e.g. Is this example good or bad?
S[+INV, SENTIMENT=?s] -> V[+AUX] NP[-WH, NUM=?n] ADJP[SENTIMENT=?s]

## and relations
S[-INV, SENTIMENT=positive] -> S[-INV, SENTIMENT=positive] CC[CONJUNCTION=and] S[-INV, SENTIMENT=positive] | S[-INV, SENTIMENT=positive] CC[CONJUNCTION=and] S[-INV, SENTIMENT=neutral] | S[-INV, SENTIMENT=neutral] CC[CONJUNCTION=and] S[-INV, SENTIMENT=positive]
S[-INV, SENTIMENT=negative] -> S[-INV, SENTIMENT=negative] CC[CONJUNCTION=and] S[-INV, SENTIMENT=negative] | S[-INV, SENTIMENT=negative] CC[CONJUNCTION=and] S[-INV, SENTIMENT=neutral] | S[-INV, SENTIMENT=neutral] CC[CONJUNCTION=and] S[-INV, SENTIMENT=negative]
S[-INV, SENTIMENT=neutral] -> S[-INV, SENTIMENT=neutral] CC[CONJUNCTION=and] S[-INV, SENTIMENT=neutral]

## but relations
S[-INV, SENTIMENT=?s] -> S[-INV, SENTIMENT=neutral] CC[CONJUNCTION=but] S[-INV, SENTIMENT=?s]
## e.g. it is a compelling story but it has low impact
S[-INV, SENTIMENT=negative] -> S[-INV, SENTIMENT=positive] CC[CONJUNCTION=but] S[-INV, SENTIMENT=negative] | S[-INV, SENTIMENT=positive] CC[CONJUNCTION=but] S[-INV, SENTIMENT=neutral]
S[-INV, SENTIMENT=positive] -> S[-INV, SENTIMENT=negative] CC[CONJUNCTION=but] S[-INV, SENTIMENT=positive] | S[-INV, SENTIMENT=negative] CC[CONJUNCTION=but] S[-INV, SENTIMENT=neutral]
## e.g. it is a compelling story , but it has low impact
S[-INV, SENTIMENT=negative] -> S[-INV, SENTIMENT=positive] COMMA CC[CONJUNCTION=but] S[-INV, SENTIMENT=negative] | S[-INV, SENTIMENT=positive] COMMA CC[CONJUNCTION=but] S[-INV, SENTIMENT=neutral]
S[-INV, SENTIMENT=positive] -> S[-INV, SENTIMENT=negative] COMMA CC[CONJUNCTION=but] S[-INV, SENTIMENT=positive] | S[-INV, SENTIMENT=negative] COMMA CC[CONJUNCTION=but] S[-INV, SENTIMENT=neutral]

## or relation
S[-INV, SENTIMENT=?s] -> S[-INV, SENTIMENT=?s] CC[CONJUNCTION=or] S[-INV, SENTIMENT=?s]
S[-INV, SENTIMENT=neutral] -> S[-INV, SENTIMENT=positive] CC[CONJUNCTION=or] S[-INV, SENTIMENT=negative] | S[-INV, SENTIMENT=negative] CC[CONJUNCTION=or] S[-INV, SENTIMENT=positive]


### -------------------------------------------------------- ###
# SBAR (subordinate clause) expansion productions
## the people *who were there*
SBAR[SENTIMENT=?s] -> NP[+WH] V[+AUX] RB[-negation, SENTIMENT=?s]
## the way *it's told by people who were there*
SBAR[SENTIMENT=?s] -> S[SENTIMENT=?s] SBAR[SENTIMENT=?s]


### -------------------------------------------------------- ###
# NP (noun phrase) expansion productions
NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] -> N[NUM=?n, PERSON=?p, SENTIMENT=?s] | NNP[NUM=?n, PERSON=?p] | PRP[NUM=?n, PERSON=?p]

## e.g. *this* is not good
NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] -> DET[NUM=?n]
## e.g. this is *an example*, *a perfect example of well-intentioned but manipulated movie making*
NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] -> DET[NUM=?n] NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] | DET[NUM=?n] NP[-WH, NUM=?n, PERSON=?p] PP[SENTIMENT=?s] | DET[NUM=?n] NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] PP
## e.g. this is a *perfect example*
NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] -> JJ[SENTIMENT=?s] NP[-WH, NUM=?n, PERSON=?p] | JJ[SENTIMENT=neutral] NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] | JJ[SENTIMENT=?s] NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s]
## e.g. movie making
NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] -> N NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s]
## e.g. shamelessly manipulative movie making
NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] -> ADJP[SENTIMENT=?s] NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] | ADJP[SENTIMENT=?s] NP[-WH, NUM=?n, PERSON=?p] | ADJP[SENTIMENT=neutral] NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s]
## e.g. the way it's told by the people who were there
NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] -> NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] SBAR[SENTIMENT=?s]
## e.g. an *example of moving making*
NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] -> NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] IN[+of] NP | NP[-WH, NUM=?n, PERSON=?p] IN[+of] NP[-WH, SENTIMENT=?s]

## e.g. it was *a mess and a hazard*, it was *a good example and a positive review*
NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] -> NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] CC[CONJUNCTION=and] NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] | NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=neutral] CC[CONJUNCTION=and] NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] | NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] CC[CONJUNCTION=and] NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=neutral]
## e.g. it was *a negative review or a positive review*
NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] -> NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] CC[CONJUNCTION=or] NP[-WH, NUM=?n, PERSON=?p] | NP[-WH, NUM=?n, PERSON=?p] CC[CONJUNCTION=or] NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s]
NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=neutral] -> NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=positive] CC[CONJUNCTION=or] NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=negative] | NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=negative] CC[CONJUNCTION=or] NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=positive]
NP[+WH, NUM=?n, PERSON=?p, SENTIMENT=neutral] -> 'who'

### -------------------------------------------------------- ###
# VP (verb phrase) expansion productions
VP[TENSE=?t, NUM=?n, PERSON=?p, SENTIMENT=?s] -> V[TENSE=?t, NUM=?n, PERSON=?p, SUBCAT=trans, SENTIMENT=?s] NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s]
VP[TENSE=?t, NUM=?n, PERSON=?p, SENTIMENT=?s] -> V[TENSE=?t, NUM=?n, PERSON=?p, SUBCAT=trans, SENTIMENT=?s] NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] PP | V[TENSE=?t, NUM=?n, PERSON=?p, SUBCAT=trans, SENTIMENT=?s] NP[-WH, NUM=?n, PERSON=?p, SENTIMENT=?s] COMMA PP

## e.g. this *does not have gut-wrenching impact*
VP[TENSE=?t, NUM=?n, PERSON=?p, SENTIMENT=?s] -> V[TENSE=?t, NUM=?n, PERSON=?p, +AUX] RB[+negation] VP[NUM=?n, SENTIMENT=neutral]
VP[TENSE=?t, NUM=?n, PERSON=?p, SENTIMENT=positive] -> V[TENSE=?t, NUM=?n, PERSON=?p, +AUX] RB[+negation] VP[NUM=?n, SENTIMENT=negative]
VP[TENSE=?t, NUM=?n, PERSON=?p, SENTIMENT=negative] -> V[TENSE=?t, NUM=?n, PERSON=?p, +AUX] RB[+negation] VP[NUM=?n, SENTIMENT=positive]

## e.g. this *may not have the dramatic gut-wrenching impact of other holocaust films*
VP[TENSE=?t, NUM=?n, PERSON=?p, SENTIMENT=?s] -> MD RB[+negation] VP[TENSE=?t, NUM=?n, PERSON=?p, SENTIMENT=?s]
VP[TENSE=?t, NUM=?n, PERSON=?p, SENTIMENT=positive] -> MD RB[+negation] VP[TENSE=?t, NUM=?n, PERSON=?p, SENTIMENT=negative]
VP[TENSE=?t, NUM=?n, PERSON=?p, SENTIMENT=negative] -> MD RB[+negation] VP[TENSE=?t, NUM=?n, PERSON=?p, SENTIMENT=positive]

## e.g. it*'s told by the people*
VP[TENSE=?t, NUM=?n, PERSON=?p, SENTIMENT=?s] -> V[TENSE=?t, NUM=?n, PERSON=?p, +AUX, -passive] V[SUBCAT=trans, -AUX, +passive] PP[SENTIMENT=?s]

## e.g. He *gave her a sweater*
VP[TENSE=?t, NUM=?n, PERSON=?p, SENTIMENT=?s] -> V[TENSE=?t, NUM=?n, PERSON=?p, SUBCAT=dative, SENTIMENT=?s] NP[-WH, NUM=?n, PERSON=?p] NP[-WH, SENTIMENT=?s]

## e.g. it *was too long but entertaining*
VP[TENSE=?t, NUM=?n, PERSON=?p, SENTIMENT=?s] -> V[TENSE=?t, NUM=?n, PERSON=?p, +AUX, -passive] JJ[SENTIMENT=?s] | V[TENSE=?t, NUM=?n, PERSON=?p, +AUX, -passive] ADJP[SENTIMENT=?s]


### -------------------------------------------------------- ###
# PP (prepositional phrase) expansion productions
PP[SENTIMENT=?s] -> IN[-of] NP[-WH, SENTIMENT=?s] | RB[-negation] IN[-of] IN[+of] NP[-WH, SENTIMENT=?s]


### -------------------------------------------------------- ###
# ADJP (adjective phrase) expansion productions
## e.g. dull and scary and boring
ADJP[SENTIMENT=?s] -> JJ[SENTIMENT=?s] CC[CONJUNCTION=and] JJ[SENTIMENT=?s] | JJ[SENTIMENT=?s] CC[CONJUNCTION=and] ADJP[SENTIMENT=?s]
## e.g. dull, scary and boring
ADJP[SENTIMENT=?s] -> JJ[SENTIMENT=?s] COMMA ADJP[SENTIMENT=?s]
## e.g. well-intentioned but manipulative
ADJP[SENTIMENT=?s] -> JJ CC[CONJUNCTION=but] JJ[SENTIMENT=?s]
## e.g. well-intentioned , but shamelessly manipulative
ADJP[SENTIMENT=?s] -> JJ COMMA CC[CONJUNCTION=but] JJ[SENTIMENT=?s] | JJ COMMA CC[CONJUNCTION=but] ADJP[SENTIMENT=?s]
## e.g. good or bad
ADJP[SENTIMENT=?s] -> JJ[SENTIMENT=?s] CC[CONJUNCTION=or] JJ[SENTIMENT=?s]
ADJP[SENTIMENT=neutral] -> JJ[SENTIMENT=positive] CC[CONJUNCTION=or] JJ[SENTIMENT=negative] | JJ[SENTIMENT=negative] CC[CONJUNCTION=or] JJ[SENTIMENT=positive]
## e.g. shamelessly manipulative
ADJP[SENTIMENT=?s] -> RB[-negation, SENTIMENT=?s] JJ[SENTIMENT=?s]
## e.g. too long but entertaining
ADJP[SENTIMENT=?s] -> RB[-negation] ADJP[SENTIMENT=?s]


# ###################
# Lexical Productions
# ###################

# Determiners
DET[NUM=sg] -> 'a' | 'an' | 'this' | 'This' | 'the'

### -------------------------------------------------------- ###
# Punctuations
COMMA -> ',' | '.'

### -------------------------------------------------------- ###
# Nouns
N[NUM=sg, SENTIMENT=neutral] -> 'story' | 'impact' | 'example' | 'movie' | 'making' | 'way' | 'sweater'
N[NUM=sg, SENTIMENT=negative] -> 'holocaust' | 'mess' | 'hazard'
N[NUM=pl, SENTIMENT=neutral] -> 'films' | 'people'


### -------------------------------------------------------- ###
# Possessive Pronoun
PRP[NUM=sg, PERSON=1] -> 'I'
PRP[NUM=sg, PERSON=3] -> 'it' | 'It' | 'He' | 'her'

### -------------------------------------------------------- ###
# Prepositions
IN[-of] -> 'with' | 'because' | 'by'
IN[+of] -> 'of'

### -------------------------------------------------------- ###
# Coordinating Conjunction
CC[CONJUNCTION=and] -> 'and' | 'And'
CC[CONJUNCTION=but] -> 'but' | 'But'
CC[CONJUNCTION=or] -> 'or' | 'Or'

### -------------------------------------------------------- ###
# Adjective
JJ[SENTIMENT=positive] -> 'compelling' | 'perfect' | 'well-intentioned' | 'entertaining'
JJ[SENTIMENT=negative] -> 'manipulative' | 'rancid' | 'ugly' | 'dull' | 'scary'
JJ[SENTIMENT=neutral] -> 'dramatic' | 'gut-wrenching' | 'low' | 'other' | 'long'

### -------------------------------------------------------- ###
# Adverb
RB[+NEGATION] -> 'not' | 'Not' | "n't"
RB[-NEGATION, SENTIMENT=neutral] -> 'mainly' | 'too' | 'there' | 'here'
RB[-NEGATION, SENTIMENT=negative] -> 'shamelessly'

### -------------------------------------------------------- ###
# Verb
V[TENSE=pres, NUM=sg, PERSON=3, +AUX] -> 'is' | "'s" | 'does'
V[TENSE=pres, +AUX] -> 'may'
V[TENSE=past, NUM=sg, +AUX] -> 'was'
V[TENSE=past, NUM=pl, +AUX] -> 'were'
V[TENSE=pres, SUBCAT=trans, NUM=sg, PERSON=3, -AUX] -> 'has'
V[TENSE=pres, SUBCAT=trans, -AUX] -> 'have'
V[TENSE=past, SUBCAT=trans, -AUX] -> 'saw'
V[TENSE=past, SUBCAT=trans, -AUX] -> 'told'
V[TENSE=past, SUBCAT=dative, -AUX] -> 'gave'